#! /bin/csh -f
#
#
if ( $#argv == 0 ) then
  echo "Usage: $0 progname key1=def1 ..."
  echo "version 24-jan-2012"
  echo "Write template NEMO program based on keywords and default values"
  echo "Output will be in a file progname.c, which cannot be overwritten"
  
  exit 0
endif

set prog=$1
set name=$prog.c
set date=`date +%d-%b-%Y` 
shift argv

if ( -e $name ) then
  echo File $name already exists, will not overwrite it.
  exit 0
endif

alias e echo

e '/*' > $name
e ' *  TEMPLATE: created by NEMO ' >> $name
e ' */' >> $name
e '' >> $name
e '#include <nemo.h>' 		>> $name
e '' >> $name
e 'string defv[] = {'		>> $name
foreach a ($argv)
e '    "'$a'\n    some help",'	>> $name
end
e '    "VERSION=0.0\n       '$date' XYZ",'		>> $name
e '    NULL,'			>> $name
e '};'				>> $name
e '' >> $name
e 'string usage="TEMPLATE program usage -- fill in yourself";' >> $name
e '' >> $name
#    break the CVS ID string in two pieces to fool cvs 
#    'string cvsid="$Id$";' >> $name
e -n 'string cvsid="$Id' >> $name
e    ':$";'              >> $name
e '' >> $name
e 'void nemo_main()'		>> $name
e '{'				>> $name
e '  /* your code goes here */' >> $name
e '  /* e.g.   string s = getparam("s");  */'  >> $name
e '  /* e.g.   int    i = getiparam("i"); */'  >> $name
e '  /* e.g.   real   r = getrparam("r"); */'  >> $name
e '  /* e.g.   double d = getdparam("d"); */'  >> $name
e '  /* followed by lots of lovely calculations */' >> $name
e '  warning("New template NEMO program");'         >> $name
e '  /*'                                            >> $name
foreach a ($argv)
    set k=`echo $a | awk -F= '{print $1}'`
e '    'string $k' = getparam("'$k'");'             >> $name

end
e '   */'                                           >> $name

e '  /*'                                            >> $name
e '   *  other useful things:'                      >> $name
e '   *    if (hasvalue("xxx") && \!hasvalue("yyy"))'>> $name
e '   *        error("need value for yyy");'        >> $name
e '   */'                                           >> $name


e '}'				>> $name

echo "All done, $name created. You can compile it using the command"
echo "        bake $prog"
echo "  or:   mknemo $prog"
echo "When done, don't forget to create a template man page:"
echo '        $NEMO/src/scripts/mkman' "$prog > "'$NEMO'"/man/man1/$prog.1"
echo '//      $NEMO/src/scripts/mkman' "$prog > "'$NEMO'"/man/man1/$prog.1"  >> $name
